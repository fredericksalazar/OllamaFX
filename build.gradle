plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.14'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.org.ollamafx'
version = '0.2.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.github.ollama4j:ollama4j:1.0.100"
    implementation 'org.fxmisc.richtext:richtextfx:0.11.0'
    implementation 'org.jsoup:jsoup:1.17.2'
    implementation 'io.github.mkpaz:atlantafx-base:2.0.1'
    implementation 'com.github.oshi:oshi-core-java11:6.4.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation 'org.commonmark:commonmark:0.21.0'

    // Cross-platform natives for Shadow JAR
    runtimeOnly "org.openjfx:javafx-graphics:21:linux"
    runtimeOnly "org.openjfx:javafx-graphics:21:win"
    runtimeOnly "org.openjfx:javafx-base:21:linux"
    runtimeOnly "org.openjfx:javafx-base:21:win"
    runtimeOnly "org.openjfx:javafx-controls:21:linux"
    runtimeOnly "org.openjfx:javafx-controls:21:win"
    runtimeOnly "org.openjfx:javafx-fxml:21:linux"
    runtimeOnly "org.openjfx:javafx-fxml:21:win"
    runtimeOnly "org.openjfx:javafx-fxml:21:linux"
    runtimeOnly "org.openjfx:javafx-fxml:21:win"
    runtimeOnly "org.openjfx:javafx-web:21:linux"
    runtimeOnly "org.openjfx:javafx-web:21:win"
    runtimeOnly "org.openjfx:javafx-media:21:linux"
    runtimeOnly "org.openjfx:javafx-media:21:win"

    // Mac & Apple Silicon Support
    runtimeOnly "org.openjfx:javafx-graphics:21:mac"
    runtimeOnly "org.openjfx:javafx-graphics:21:mac-aarch64"
    runtimeOnly "org.openjfx:javafx-base:21:mac"
    runtimeOnly "org.openjfx:javafx-base:21:mac-aarch64"
    runtimeOnly "org.openjfx:javafx-controls:21:mac"
    runtimeOnly "org.openjfx:javafx-controls:21:mac-aarch64"
    runtimeOnly "org.openjfx:javafx-fxml:21:mac"
    runtimeOnly "org.openjfx:javafx-fxml:21:mac-aarch64"
    runtimeOnly "org.openjfx:javafx-web:21:mac"
    runtimeOnly "org.openjfx:javafx-web:21:mac-aarch64"
    runtimeOnly "org.openjfx:javafx-media:21:mac"
    runtimeOnly "org.openjfx:javafx-media:21:mac-aarch64"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

javafx {
    version = "21"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.web']
}

application {
    mainClass = 'com.org.ollamafx.Launcher'
    applicationDefaultJvmArgs = ['-Xdock:name=OllamaFX', '-Xdock:icon=src/main/resources/icons/icon.png']
}

tasks.withType(JavaExec) {
    if (System.getProperty('os.name').toLowerCase().contains('mac')) {
        jvmArgs '-Xdock:name=OllamaFX'
        jvmArgs "-Xdock:icon=${project.projectDir}/src/main/resources/icons/icon.png"
    }
}

processResources {
    filesMatching('**/*.properties') {
        expand(project.properties)
    }
}

shadowJar {
    archiveBaseName.set('OllamaFX')
    archiveClassifier.set('')
    archiveVersion.set(version)
    mergeServiceFiles()
    exclude 'module-info.class'
    manifest {
        attributes 'Main-Class': 'com.org.ollamafx.Launcher'
    }
}

task packageDistribution(type: Zip) {
    dependsOn shadowJar
    archiveFileName = "OllamaFX-${version}-Dist.zip"
    destinationDirectory = file("$buildDir/distributions")

    from(shadowJar.archiveFile) {
        into "."
    }
    from("scripts") {
        into "."
    }

    doLast {
        copy {
            from destinationDirectory
            into "${project.rootDir}/releases"
            include archiveFileName.get()
        }
        println "Release copied to ${project.rootDir}/releases/${archiveFileName.get()}"
    }
}

// Custom jpackage task for Mac
task createInstaller(type: Exec) {
    dependsOn shadowJar
    workingDir project.rootDir
    
    // Command to key inputs:
    // --input build/libs containing the shadow jar
    // --main-jar the shadow jar name
    // --icon the generated icns
    
    commandLine 'jpackage', 
        '--input', 'build/libs',
        '--name', 'OllamaFX',
        '--main-jar', "OllamaFX-${version}.jar",
        '--main-class', 'com.org.ollamafx.Launcher',
        '--type', 'dmg',
        '--icon', 'src/main/resources/icons/icon.icns',
        '--dest', 'releases',
        '--java-options', '--enable-preview' 
        
    doFirst {
        mkdir 'releases'
        println "Creating Installer with icon..."
    }
}